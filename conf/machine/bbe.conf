# Copyright (C) 2018-2022 SanCloud Ltd
# SPDX-License-Identifier: MIT

#@TYPE: Machine
#@NAME: SanCloud BeagleBone Enhanced Development Board
#@DESCRIPTION: Machine configuration for the BeagleBone Enhanced https://sancloud.co.uk/beaglebone-enhanced-bbe/

################################################################################
# Basic machine config

MACHINEOVERRIDES =. "beaglebone:"

IMAGE_FSTYPES = "tar.xz wic.xz wic.bmap"
WKS_FILE ?= "sancloud_bbe.wks"
WICVARS:append = " IMAGE_OVERHEAD_FACTOR"

require conf/machine/include/ti33x.inc

BBE_WIFI_DRIVERS ?= "rtl8723bu qcacld"
MACHINE_EXTRA_RDEPENDS += "u-boot-script-bbe kernel-modules ${BBE_WIFI_DRIVERS}"
SERIAL_CONSOLE = "115200 ttyS0"
MACHINE_FEATURES += "bluetooth"

################################################################################
# U-boot

PREFERRED_PROVIDER_virtual/bootloader = "u-boot-bbe"
PREFERRED_PROVIDER_u-boot = "u-boot-bbe"
EXTRA_IMAGEDEPENDS += "u-boot-bbe"

SPL_BINARY = "MLO"
UBOOT_SUFFIX = "img"
UBOOT_MACHINE = "sancloud_defconfig"

IMAGE_BOOT_FILES = ""

################################################################################
# Kernel

KERNEL_DEVICETREE = " \
    am335x-sancloud-bbe.dtb \
    "
KERNEL_EXTRA_ARGS += "LOADADDR=${UBOOT_ENTRYPOINT}"

BBE_KERNEL_PROVIDER ??= "default"
require conf/machine/include/bbe-kernel-${BBE_KERNEL_PROVIDER}.inc

BBE_SPIBOOT ??= "0"
require ${@bb.utils.contains('BBE_SPIBOOT', '1', 'conf/machine/include/bbe-spiboot.inc', '', d)}
